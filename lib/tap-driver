#! /usr/bin/env perl
# Temporary/experimental TAP test driver for Automake.
# TODO: should be rewritten portably (e.g., in awk or shell).

# ---------------------------------- #
#  Imports, static data, and setup.  #
# ---------------------------------- #

use warnings FATAL => 'all';
use strict;
use Getopt::Long ();
use TAP::Parser;

my $ME = "tap-driver";

my $USAGE = <<'END';
Usage:
  tap-driver --test-name=NAME --log-file=PATH --trs-file=PATH
             [--expect-failure={yes|no}] [--color-tests={yes|no}]
             [--enable-hard-errors={yes|no}] [--ignore-exit]
             [--diagnostic-string=STRING] [--merge|--no-merge]
             [--comments|--no-comments] [--] TEST-COMMAND
The `--test-name' and `--log-file' options are mandatory.
END

my $HELP = "$ME: TAP-aware test driver for Automake testsuite harness." .
           "\n" . $USAGE;

my $VERSION = '(experimental version)';

# Keep this in sync with `lib/am/check.am:$(am__tty_colors)'.
my %COLOR = (
  red => "\e[0;31m",
  grn => "\e[0;32m",
  lgn => "\e[1;32m",
  blu => "\e[1;34m",
  mgn => "\e[0;35m",
  brg => "\e[1m",
  std => "\e[m",
);

# ------------------- #
#  Global variables.  #
# ------------------- #

my $lineno = 0;     # Number of input lines seen so far.
my $testno = 0;     # Number of test results seen so far.
my $plan_seen = 0;  # Whether the TAP plan has been seen or not.
my $parser;         # TAP parser object (will be initialized later).

# ----------------- #
#  Option parsing.  #
# ----------------- #

my %cfg = (
  "color-tests" => 0,
  "expect-failure" => 0,
  "enable-hard-errors" => 1,
  "merge" => 0,
  "comments" => 0,
  "ignore-exit" => 0,
);

my $test_script_name = undef;
my $log_file = undef;
my $trs_file = undef;
my $diag_string = "#";

Getopt::Long::GetOptions (
    'help' => sub { print $HELP; exit 0; },
    'version' => sub { print "$ME $VERSION"; exit 0; },
    'test-name=s' => \$test_script_name,
    'log-file=s' => \$log_file,
    'trs-file=s' => \$trs_file,
    'color-tests=s'  => \&bool_opt,
    'expect-failure=s'  => \&bool_opt,
    'enable-hard-errors=s' => \&bool_opt,
    'diagnostic-string=s' => \$diag_string,
    'comments' => sub { $cfg{"comments"} = 1; },
    'no-comments' => sub { $cfg{"comments"} = 0; },
    'merge' => sub { $cfg{"merge"} = 1; },
    'no-merge' => sub { $cfg{"merge"} = 0; },
    'ignore-exit' => sub { $cfg{"ignore-exit"} = 1; },
  ) or exit 1;

# -------------- #
#  Subroutines.  #
# -------------- #

sub bool_opt ($$)
{
  my ($opt, $val) = @_;
  if ($val =~ /^(?:y|yes)\z/i)
    {
      $cfg{$opt} = 1;
    }
  elsif ($val =~ /^(?:n|no)\z/i)
    {
      $cfg{$opt} = 0;
    }
  else
    {
      die "invalid argument '$val' for option '$opt'\n";
    }
}

# Convert a boolean to a "yes"/"no" string.
sub yn ($)
{
  my $bool = shift;
  return $bool ? "yes" : "no";
}

TAP_PEEKING :
{
  my @tap_lines = ();

  sub get_tap_line ()
  {
    @tap_lines > 0 ? pop @tap_lines : $parser->next;
  }

  sub unget_tap_line ($)
  {
    push @tap_lines, @_;
  }

  sub peek_tap_line ()
  {
    my $res = get_tap_line;
    unget_tap_line ($res);
    return $res;
  }

}

TEST_RESULTS :
{
  my (@test_results, %test_results);

  sub add_test_result ($)
  {
    my $res = shift;
    push @test_results, $res;
    $test_results{$res} = 1;
  }

  sub get_test_results ()
  {
    return @test_results;
  }

  # Whether the test script should be re-run by "make recheck".
  sub must_recheck ()
  {
    return grep { !/^(?:XFAIL|PASS|SKIP)$/ } (keys %test_results);
  }

  # Whether the content of the log file associated to this test should
  # be copied into the "global" test-suite.log.
  sub copy_in_global_log ()
  {
    return grep { not $_ eq "PASS" } (keys %test_results);
  }

  # FIXME: this can certainly be improved ...
  sub get_global_test_result ()
  {
    my @results = keys %test_results;
    return "ERROR" if exists $test_results{"ERROR"};
    return "SKIP" if @results == 1 && $results[0] eq "SKIP";
    return "FAIL" if exists $test_results{"FAIL"};
    return "FAIL" if exists $test_results{"XPASS"};
    return "PASS";
  }

}

sub write_test_results ()
{
  open RES, ">", $trs_file or die "opening $trs_file: $!\n";
  print RES ":global-test-result: " . get_global_test_result . "\n";
  print RES ":recheck: " . yn (must_recheck) . "\n";
  print RES ":copy-in-global-log: " . yn (copy_in_global_log) . "\n";
  foreach my $result (get_test_results)
    {
      print RES ":test-result: $result\n";
    }
  close RES or die "closing $trs_file: $!\n";
}

sub start (@)
{
  # Redirect stderr and stdout to a temporary log file.  Save the
  # original stdout stream, since we need it to print testsuite
  # progress output.
  open LOG, ">", $log_file or die "opening $log_file: $!\n";
  open OLDOUT, ">&STDOUT" or die "duplicating stdout: $!\n";
  open STDOUT, ">&LOG" or die "redirecting stdout: $!\n";
  open STDERR, ">&LOG" or die "redirecting stderr: $!\n";
  $parser = TAP::Parser->new ({ exec => \@_, merge => $cfg{merge} });
  $parser->ignore_exit(1) if $cfg{"ignore-exit"};
}

sub finish ()
{
  write_test_results;
  close LOG or die "closing $log_file: $!\n";
  exit 0;
}

sub stringify_test_result ($)
{
  my $result = shift;
  my $PASS = $cfg{"expect-failure"} ? "XPASS": "PASS";
  my $FAIL = $cfg{"expect-failure"} ? "XFAIL": "FAIL";
  if ($result->is_unplanned || $result->number != $testno)
    {
      return "ERROR";
    }
  elsif (!$result->directive)
    {
      return $result->is_ok ? $PASS: $FAIL;
    }
  elsif ($result->has_todo)
    {
      return $result->is_actual_ok ? "XPASS" : "XFAIL";
    }
  elsif ($result->has_skip)
    {
      return $result->is_ok ? "SKIP" : $FAIL;
    }
  die "INTERNAL ERROR"; # NOTREACHED
}

sub colored ($$)
{
  my ($color_name, $text) = @_;
  return  $COLOR{$color_name} . $text . $COLOR{'std'};
}

sub decorate_result ($)
{
  my $result = shift;
  return $result unless $cfg{"color-tests"};
  my %color_for_result =
    (
      "ERROR" => 'mgn',
      "PASS"  => 'grn',
      "XPASS" => 'red',
      "FAIL"  => 'red',
      "XFAIL" => 'lgn',
      "SKIP"  => 'blu',
    );
  if (my $color = $color_for_result{$result})
    {
      return colored ($color, $result);
    }
  else
    {
      return $result; # Don't colorize unknown stuff.
    }
}

sub report ($;$)
{
  my ($msg, $result, $explanation) = (undef, @_);
  if ($result =~ /^(?:X?(?:PASS|FAIL)|SKIP|ERROR)/)
    {
      $msg = ": $test_script_name";
      add_test_result $result;
    }
  elsif ($result eq "#")
    {
      $msg = " $test_script_name:";
    }
  else
    {
      die "INTERNAL ERROR"; # NOTREACHED
    }
  $msg .= " $explanation" if defined $explanation;
  $msg .= "\n";
  # Output on console might be colorized.
  print OLDOUT decorate_result ($result) . $msg;
  # Log the result in the log file too, to help debugging (this is
  # especially true when said result is a TAP error or "Bail out!").
  print $result . $msg;
}

sub testuite_error ($)
{
  report "ERROR", "- $_[0]";
}

sub handle_tap_test ($)
{
  $testno++;
  my $test = shift;

  my $test_result = stringify_test_result $test;
  my $string = $test->number;
  
  if (my $description = $test->description)
    {
      $string .= " $description";
    }
  if ($test->is_unplanned)
    {
      $string .= " # UNPLANNED";
    }
  elsif ($test->number != $testno)
    {
      $string .= " # OUT-OF-ORDER (expecting $testno)";
    }
  elsif (my $directive = $test->directive)
    {
      $string .= " # $directive";
      if (my $explanation = $test->explanation)
        {
          $string .= " $explanation";
        }
    }
  report $test_result, $string;
}

sub handle_tap_plan ($)
{
  my $plan = shift;
  testuite_error "multiple test plans" if $plan_seen;
  $plan_seen = 1;
  # TAP plan must be either in the first or in the last line.
  if ($lineno > 1 && peek_tap_line)
    {
      testuite_error "test plan in middle of output";
      return;
    }
  # Nothing more to do, unless the plan contains a SKIP directive.
  return
    if not defined $plan->directive && length ($plan->directive) > 0;
  my $explanation = $plan->explanation ?
                    "- " . $plan->explanation : undef;
  report "SKIP", $explanation;
  finish;
}

sub handle_tap_bailout ($)
{
  my ($bailout, $msg) = ($_[0], "Bail out!");
  $msg .= " " . $bailout->explanation if $bailout->explanation;
  testuite_error $msg;
  finish;
}

sub extract_tap_comment ($)
{
  local $_ = shift;
  if (/^\Q$diag_string\E(.*)$/o)
    {
      (my $comment = $1) =~ s/(?:^\s*|\s*$)//;
      return $comment;
    }
  return "";
}

sub main (@)
{
  start @_;

  while (defined (my $cur = get_tap_line))
    {
      # Verbatim copy any input line into the log file.
      print $cur->raw . "\n";
      $lineno++;
      if ($cur->is_plan)
        {
          handle_tap_plan ($cur);
        }
      elsif ($cur->is_test)
        {
          handle_tap_test ($cur);
        }
      elsif ($cur->is_bailout)
        {
          handle_tap_bailout ($cur);
        }
      elsif ($cfg{comments})
        {
          my $comment = extract_tap_comment ($cur->raw);
          report "#", "$comment" if length $comment;
       }
    }
  if (!$plan_seen)
    {
      testuite_error "missing test plan";
    }
  elsif ($parser->tests_planned != $parser->tests_run)
    {
      my ($planned, $run) = ($parser->tests_planned, $parser->tests_run);
      my $bad_amount = $run > $planned ? "many" : "few";
      testuite_error (sprintf "too %s tests run (expected %d, got %d)",
                              $bad_amount, $planned, $run);
    }
  # TODO: we should probably use $parser->wait here, to catch signals too
  if ($parser->exit != 0)
  {
    my $msg = sprintf "exited with status %d", $parser->exit;
    if ($cfg{"ignore-exit"})
      {
        # Log the exit status of the script anyway, even if it is not
        # considered to be an error, to help debugging.
        print "INFO: $test_script_name - $msg\n";
      }
    else
      {
        testuite_error $msg;;
      }
  }
  finish;
}

# ----------- #
#  Main code. #
# ----------- #

main @ARGV;

# vim: ft=perl ts=4 sw=4 et
